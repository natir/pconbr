###############################################################################
# Manage input and output                                                     #
###############################################################################
inputs = {
    "s_pneumoniae": ["data/SRR8556426.fasta"],
    "c_vartiovaarae": ["data/ERR1877966.fasta", "data/ERR1877967.fasta", "data/ERR1877968.fasta", "data/ERR1877969.fasta", "data/ERR1877970.fasta"],
    "e_coli": ["data/SRR8494940.fasta"],
}

outputs = {
    "s_pneumoniae": "count/s_pneumoniae",
    "c_vartiovaarae": "count/c_vartiovaarae",
    "e_coli": "count/e_coli",
}

def generate_output(dataset, suffix):
    return outputs[dataset] + "." + suffix

###############################################################################
# Section SSIK                                                                #
###############################################################################
rule ssik:
    input:
        lambda wildcards: inputs[wildcards.dataset_name]
    output:
        "count/{dataset_name}.ssik"
    benchmark:
        "benchmark/ssik/{dataset_name}.tsv"
    shell:
        "ssik count -i {input} -o {output} -k 17 -m 1"

rule all_ssik:
    input:
        generate_output("s_pneumoniae", "ssik"),
        generate_output("c_vartiovaarae", "ssik"),
        generate_output("e_coli", "ssik"),
        
###############################################################################
# Section KMC                                                                 #
###############################################################################
rule kmc:
    input:
        lambda wildcards: inputs[wildcards.dataset_name]
    output:
        "count/{dataset_name}.kmc"
    benchmark:
        "benchmark/kmc/{dataset_name}.tsv"
    shell:
        " && ".join([
            "mkdir -p kmc_workdir/{wildcards.dataset_name}",
            "mkdir -p kmc_file_input",
            "echo {input} > kmc_file_input/{wildcards.dataset_name}.lst",
            "sed -i 's/ /\\n/g' kmc_file_input/{wildcards.dataset_name}.lst",
            "kmc -k17 -t1 -fa @kmc_file_input/{wildcards.dataset_name}.lst {output} kmc_workdir/{wildcards.dataset_name}",
            ])

rule all_kmc:
    input:
        generate_output("s_pneumoniae", "kmc"),
        generate_output("c_vartiovaarae", "kmc"),
        generate_output("e_coli", "kmc"),

###############################################################################
# Section JELLYFISH                                                           #
###############################################################################
rule jellyfish:
    input:
        lambda wildcards: inputs[wildcards.dataset_name]
    output:
        "count/{dataset_name}.jellyfish"
    benchmark:
        "benchmark/jellyfish/{dataset_name}.tsv"
    shell:
        "jellyfish count -m17 -t1 -s10G -C -o {output} {input}"
          
rule all_jellyfish:
    input:
        generate_output("s_pneumoniae", "jellyfish"),
        generate_output("c_vartiovaarae", "jellyfish"),
        generate_output("e_coli", "jellyfish"),
        
rule all:
    input:
        rules.all_ssik.input,
        rules.all_kmc.input,
        rules.all_jellyfish.input,


