rule kmrf:
    input:
        "data/{dataset}/{type}.fasta"

    output:
        "filter/{dataset}/kmrf/{type}.k{kmer_size}.r{ratio}.fasta"

    log:
        "log/filter/kmrf/{dataset}_{type}.k{kmer_size}.r{ratio}.txt"
        
    benchmark:
        "filter/bench/kmrf/{dataset}_{type}.k{kmer_size}.r{ratio}.tsv"

    params:
        ratio = lambda wcd: int(wcd.ratio)/100,
            
    resources:
        mem_mb = lambda wcd: pcon_memory_usage(int(wcd.kmer_size)) + br_memory_usage(int(wcd.kmer_size))

    threads:
        config['max_threads']
        
    shell:
        "kmrf -i {input} -o {output} -k {wildcards.kmer_size} -t {threads} -r {params.ratio} -vvv 2>&1 > {log}"


rule filtlong:
    input:
        reads = "data/{dataset}/{type}.fasta",
        ref = "data/{dataset}/reference.fasta"

    output:
        "filter/{dataset}/filtlong/{type}{params}.q{quality}.fasta"

    log:
        "log/filter/filtlong/{dataset}_{type}{params}.q{quality}.txt"
        
    benchmark:
        "filter/bench/filtlong/{dataset}_{type}{params}.q{quality}.tsv"
    
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/filtlong.yaml"
        
    shell:
        "filtlong --min_mean_q {wildcards.quality} -a {input.ref} {input.reads} > {output} 2> {log}"


rule redbean_raw:
    input:
        "data/{dataset}/reads.fasta"

    output:
        "filter/{dataset}/redbean/raw/reads/dbg.raw.fa"

    log:
        "log/filter/readbean/{dataset}_raw.txt"
        
    params:
        prefix = "filter/{dataset}/redbean/raw/reads/dbg",
        genome_size = lambda wcd: config["genomeSize"][wcd.dataset] if wcd.dataset in config["genomeSize"] else config["genomeSize"]["bacteria"],
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/redbean.yaml"
        
    shell:
        """
        wtdbg2 -x ont -g {params.genome_size} -i {input} -t {threads} -fo {params.prefix} 2>&1 > {log}
        wtpoa-cns -t {threads} -i {params.prefix}.ctg.lay.gz -fo {output} 2>&1 >> {log}
        """
        
        
rule readbean:
    input:
        "filter/{dataset}/{filter}/{type}{params}.fasta"

    output:
        "filter/{dataset}/redbean/{filter}/{type}{params}/dbg.raw.fa"

    log:
        "log/filter/readbean/{dataset}_{filter}_{type}{params}.txt"
        
    params:
        prefix = "filter/{dataset}/redbean/{filter}/{type}{params}/dbg",
        genome_size = lambda wcd: config["genomeSize"][wcd.dataset] if wcd.dataset in config["genomeSize"] else config["genomeSize"]["bacteria"],
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/redbean.yaml"
        
    shell:
        """
        wtdbg2 -x ont -g {params.genome_size} -i {input} -t {threads} -fo {params.prefix} 2>&1 > {log}
        wtpoa-cns -t {threads} -i {params.prefix}.ctg.lay.gz -fo {output} 2>&1 >> {log}
        """

        
rule quast_filter:
    input:
        asm = "filter/{dataset}/redbean/{filter}/{type}{params}/dbg.raw.fa",
        ref = "data/{dataset}/reference.fasta"

    output:
        "filter/{dataset}/quast/{filter}/{type}{params}/report.txt"

    log:
        "log/filter/quast/{dataset}_{filter}_{type}{params}.txt"
        
    params:
        prefix = "filter/{dataset}/quast/{filter}/{type}{params}/"
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/quast.yaml"
        
    shell:
        "quast --min-identity 80 -t {threads} -o {params.prefix} -r {input.ref} {input.asm} 2>&1 > {log}"


rule evaluate_filter:
    input:
        reads = "filter/{dataset}/{filter}/{type}{params}.fasta",
        ref = "data/{dataset}/reference.fasta"
        
    output:
        "filter/{dataset}/identity/{filter}/{type}{params}.tsv"

    log:
        "log/filter/identity/{dataset}_{filter}_{type}{params}.txt"
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/minimap.yaml"
        
    shell:
        "minimap2 -x map10k -t {threads} -c {input.ref} {input.reads} 2> {log} | ./script/read_length_identity.py > {output} 2>> {log}"

        
rule filter_error_rate:
    input:
        reads = "filter/{dataset}/{filter}/{type}{params}.fasta",
        ref = "data/{dataset}/reference.fasta"

    output:
        "filter/{dataset}/{filter}/{type}{params}.stats"

    log:
        "log/filter/error_rate/{dataset}_{filter}_{type}{params}.txt"
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/minimap.yaml"
        
    shell:
        "minimap2 -x map10k -t {threads} -a {input.ref} {input.reads} 2> {log} | samtools stats 2> {log} | grep '^SN' > {output} 2> {log}"
        
        
rule filter_all:
    input:
        filter_files("bacteria"),
        filter_files("bacteria5"),
        filter_files("bacteria7"),
        filter_files("yeast"),
        filter_files("metagenome"),
        (filter_files(f"synthetic.e{mean_id}.c{config['coverage']}") for mean_id in conf_range("identity")),
        (filter_files(f"celegans.e{config['celegans_identity']}.c{coverage}") for coverage in conf_range("celegans_coverage")),
