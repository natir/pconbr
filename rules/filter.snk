rule kmrf:
    input:
        "data/{dataset}/{type}.fasta"

    output:
        "filter/{dataset}/kmrf/{type}.k{kmer_size}.r{ratio}.fasta"

    benchmark:
        "filter/bench/kmrf/{dataset}_{type}.k{kmer_size}.r{ratio}.tsv"
        
    resources:
        mem_mb = lambda wcd: pcon_memory_usage(int(wcd.kmer_size)) + br_memory_usage(int(wcd.kmer_size))

    threads:
        32
        
    shell:
        "kmrf -i {input} -o {output} -k {wildcards.kmer_size} -t {threads} -r {wildcards.ratio}"


rule filtlong:
    input:
        reads = "data/{dataset}/{type}.fasta",
        ref = "data/{dataset}/reference.fasta"

    output:
        "filter/{dataset}/filtlong/{type}.q{quality}.fasta"

    benchmark:
        "filter/bench/kmrf/{dataset}_{type}.q{quality}.tsv"
    
    threads:
        32

    conda:
        f"../{config['env_mode']}/filtlong.yaml"
        
    shell:
        "filtlong --min_mean_q {wildcards.quality} -a {input.ref} {input.reads} > {output}"

        
rule redbean:
    input:
        "filter/{dataset}/{filter}/{type}{params}.fasta"

    output:
        "filter/{dataset}/redbean/{filter}/{type}{params}/dbg.raw.fa"

    params:
        prefix = "filter{dataset}/redbean/{filter}/{type}{params}/dbg",
        genome_size = lambda wcd: config["genomeSize"][wcd.dataset],
        
    threads:
        32

    conda:
        f"../{config['env_mode']}/redbean.yaml"
        
    shell:
        """
        wtdbg2 -x ont -g {params.genome_size} -i {input} -t {threads} -fo {params.prefix}
        wtpoa-cns -t {threads} -i {params.prefix}.ctg.lay.gz -fo {output}
        """

        
rule quast_filter:
    input:
        asm = "filter/{dataset}/redbean/{filter}/{type}{params}/dbg.raw.fa",
        ref = "data/{dataset}/reference.fasta"

    output:
        "filter/{dataset}/quast/{filter}/{type}{params}/report.txt"

    params:
        prefix = "filter/quast/{dataset}/{type}{params}/"
        
    threads:
        32

    conda:
        f"../{config['env_mode']}/quast.yaml"
        
    shell:
        "quast --min-identity 80 -t {threads} -o {params.prefix} -r {input.ref} {input.asm}"


rule evaluate_filter:
    input:
        reads = "filter/{dataset}/{filter}/{type}{params}.fasta",
        ref = "data/{dataset}/reference.fasta"
        
    output:
        "filter/{dataset}/identity/{filter}/{type}{params}.tsv"

    threads:
        32

    conda:
        f"../{config['env_mode']}/miniasm.yaml"
        
    shell:
        "minimap2 -x map10k -t {threads} -c {input.ref} {input.reads} | ./script/read_length_identity.py > {output}"


rule filter_bacteria:
    input:
        # synthetic
        [f"filter/bacteria/quast/kmrf/synthetic.e{mean_id}.c{config['coverage']}.k{kmer_size}.r{ratio/10}/report.txt" for mean_id in conf_range("identity") for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],
        [f"filter/bacteria/identity/kmrf/synthetic.e{mean_id}.c{config['coverage']}.k{kmer_size}.r{ratio/10}.tsv" for mean_id in conf_range("identity") for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],

        [f"filter/bacteria/quast/filtlong/synthetic.e{mean_id}.c{config['coverage']}.q{quality}/report.txt" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],
        [f"filter/bacteria/identity/filtlong/synthetic.e{mean_id}.c{config['coverage']}.q{quality}.tsv" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],
        
        # real
        [f"filter/bacteria/quast/kmrf/reads.k{kmer_size}.r{ratio/10}/report.txt" for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],
        [f"filter/bacteria/identity/kmrf/reads.k{kmer_size}.r{ratio/10}.tsv" for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],

        [f"filter/bacteria/quast/filtlong/reads.q{quality}/report.txt" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],
        [f"filter/bacteria/identity/filtlong/reads.q{quality}.tsv" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],

        
rule filter_yeast:
    input:
        # real
        [f"filter/yeast/quast/kmrf/reads.k{kmer_size}.r{ratio/10}/report.txt" for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],
        [f"filter/yeast/identity/kmrf/reads.k{kmer_size}.r{ratio/10}.tsv" for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],

        [f"filter/yeast/quast/filtlong/reads.q{quality}/report.txt" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],
        [f"filter/yeast/identity/filtlong/reads.q{quality}.tsv" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],

        
rule filter_metagenome:
    input:
        # real
        [f"filter/metagenome/quast/kmrf/reads.k{kmer_size}.r{ratio/10}/report.txt" for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],
        [f"filter/metagenome/identity/kmrf/reads.k{kmer_size}.r{ratio/10}.tsv" for kmer_size in conf_range("kmer", step=2) for ratio in conf_range("ratio")],

        [f"filter/metagenome/quast/filtlong/reads.q{quality}/report.txt" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],
        [f"filter/metagenome/identity/filtlong/reads.q{quality}.tsv" for mean_id in conf_range("identity") for quality in conf_range("filtlong_qual")],

        
rule filter_all:
    input:
        rules.filter_bacteria.input,
        rules.filter_yeast.input,
        rules.filter_metagenome.input,
