rule pcon_polish:
    input:
        "data/{dataset}/reads.fasta"
        
    output:
        "polish/{dataset}/reads.k{kmer_size}.a{abundance}.solid"

    log:
        "log/polish/pcon_dump/{dataset}_reads.k{kmer_size}.a{abundance}.txt"
                
    resources:
        mem_mb = lambda wcd: pcon_memory_usage(int(wcd.kmer_size)) + br_memory_usage(int(wcd.kmer_size))
    
    threads:
        config['max_threads']

    shell:
        "pcon -vvv -t {threads} count -i {input} -s {output} -k {wildcards.kmer_size} -a {wildcards.abundance} 2>&1 > {log}"


rule miniasm:
    input:
        "data/{dataset}/reads.fasta"

    output:
        ovl = "data/{dataset}/miniasm/asm.paf",
        graph = "data/{dataset}/miniasm/asm.gfa",
        asm = "data/{dataset}/miniasm/asm.fasta",

    log:
        "log/polish/miniasm/{dataset}.txt"            

    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/miniasm.yaml"
        
    shell:
        """
        minimap2 -t {threads} -x ava-ont {input} {input} > {output.ovl} 2> {log}
        miniasm -f {input} {output.ovl} > {output.graph} 2>> {log}
        ./script/gfaminiasm2fasta.py {output.graph} {output.asm} 2>> {log}
        """


rule br_polish:
    input:
        solid = "polish/{dataset}/reads.k{kmer_size}.a{abundance}.solid",
        asm = "data/{dataset}/miniasm/asm.fasta",

    output:
        "polish/{dataset}/polish.k{kmer_size}.a{abundance}.fasta"

    log:
        "log/polish/br_polish/{dataset}.k{kmer_size}.a{abundance}.txt"
                
    resources:
        mem_mb = lambda wcd: br_memory_usage(int(wcd.kmer_size))

    threads:
        config['max_threads']

    shell:
        "br -s {input.solid} -i {input.asm} -o {output} -t {threads} -m one greedy gap_size graph 2> {log}"


rule not_polish:
    input:
        "data/{dataset}/miniasm/asm.fasta",

    output:
        "polish/{dataset}/polish.fasta"

    log:
        "log/polish/not_polish/{dataset}.txt"
        
    shell:
        "ln -s $(pwd)/{input} $(pwd)/{output} 2>&1 > {log}"
    

rule quast_polish:
    input:
        asm = "polish/{dataset}/polish{params}.fasta",
        ref = "data/{dataset}/reference.fasta"

    output:
        "polish/{dataset}/quast/polish{params}/report.txt"

    log:
        "log/polish/quast/{dataset}{params}.txt"    

    params:
        prefix = "polish/{dataset}/quast/polish{params}/"
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/quast.yaml"
        
    shell:
        "quast --min-identity 80 -t {threads} -o {params.prefix} -r {input.ref} {input.asm} 2>&1 > {log}"


rule polish_all:
    input:
        (polish_files(d) for d in get_dataset()),
      
