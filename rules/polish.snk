rule pcon_count:
    input:
        "data/{dataset}/reads.fasta"

    output:
        "polish/{dataset}/reads.k{kmer_size}.pcon"

    log:
        "log/polish/pcon_count/{dataset}_reads.k{kmer_size}.txt"
        
    resources:
        mem_mb = lambda wcd: pcon_memory_usage(int(wcd.kmer_size))
        
    threads:
        config['max_threads']

    shell:
        "pcon count -i {input} -k {wildcards.kmer_size} -o {output} -t {threads} -vvv 2>&1 > {log}"

        
rule pcon_dump:
    input:
        "polish/{dataset}/reads.k{kmer_size}.pcon"

    output:
        "polish/{dataset}/reads.k{kmer_size}.a{abundance}.solid"

    log:
        "log/polish/pcon_dump/{dataset}_reads.k{kmer_size}.a{abundance}.txt"
                
    resources:
        mem_mb = lambda wcd: pcon_memory_usage(int(wcd.kmer_size)) + br_memory_usage(int(wcd.kmer_size))
    
    shell:
        "pcon dump -i {input} -s {output} -vvv 2>&1 > {log}"


rule miniasm:
    input:
        "data/{dataset}/reads.fasta"

    output:
        ovl = "data/{dataset}/miniasm/asm.paf",
        graph = "data/{dataset}/miniasm/asm.gfa",
        asm = "data/{dataset}/miniasm/asm.fasta",

    log:
        "log/polish/miniasm/{dataset}.txt"            

    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/miniasm.yaml"
        
    shell:
        """
        minimap2 -t {threads} -x ava-ont {input} {input} > {output.ovl} 2> {log}
        miniasm -f {input} {output.ovl} > {output.graph} 2>> {log}
        ./script/gfaminiasm2fasta.py {output.graph} {output.asm} 2>> {log}
        """


rule br_polish:
    input:
        solid = "polish/{dataset}/reads.k{kmer_size}.a{abundance}.solid",
        asm = "data/{dataset}/miniasm/asm.fasta",

    output:
        "polish/{dataset}/polish.k{kmer_size}.a{abundance}.fasta"

    log:
        "log/polish/br_polish/{dataset}.k{kmer_size}.a{abundance}.txt"
                
    resources:
        mem_mb = lambda wcd: br_memory_usage(int(wcd.kmer_size))

    threads:
        config['max_threads']

    shell:
        "br -s {input.solid} -i {input.asm} -o {output} -t {threads} -m one greedy gap_size graph 2> {log}"


rule quast_polish:
    input:
        asm = "polish/{dataset}/polish.k{kmer_size}.a{abundance}.fasta",
        ref = "data/{dataset}/reference.fasta"

    output:
        "polish/{dataset}/quast/polish.k{kmer_size}.a{abundance}/report.txt"

    log:
        "log/polish/quast/{dataset}.k{kmer_size}.a{abundance}.txt"    

    params:
        prefix = "polish/{dataset}/quast/polish.k{kmer_size}.a{abundance}/"
        
    threads:
        config['max_threads']

    conda:
        f"../{config['env_mode']}/quast.yaml"
        
    shell:
        "quast --min-identity 80 -t {threads} -o {params.prefix} -r {input.ref} {input.asm} 2>&1 > {log}"


rule polish_bacteria:
    input:
        # real
        [f"polish/bacteria/quast/polish.k{kmer_size}.a{abundance}/report.txt" for kmer_size in conf_range("kmer", step=2) for abundance in conf_range("abundance")],

        
rule polish_yeast:
    input:
        # real
        [f"polish/yeast/quast/polish.k{kmer_size}.a{abundance}/report.txt" for kmer_size in conf_range("kmer", step=2) for abundance in conf_range("abundance")],
        

rule polish_metagenome:
    input:
        # real
        [f"polish/metagenome/quast/polish.k{kmer_size}.a{abundance}/report.txt" for kmer_size in conf_range("kmer", step=2) for abundance in conf_range("abundance")],


rule polish_all:
    input:
        rules.polish_bacteria.input,
        rules.polish_yeast.input,
        rules.polish_metagenome.input,
        
